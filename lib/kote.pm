package kote;
use strict;
use warnings;

our $VERSION = "0.01";

use Carp qw(croak);
use Scalar::Util qw(blessed);

use Types::TypeTiny ();
use Eval::TypeTiny qw( set_subname type_to_coderef );

use Type::Kote;

# If $STRICT is 0, type->create skips check value and convert to immutable reference
our $STRICT = 1;

# kote name must be CamelCase
my $normal_kote_name = qr/^[A-Z][a-zA-Z0-9]*$/;

my %forbidden_kote_name = map { $_ => 1 } qw{
    BEGIN CHECK DESTROY END INIT UNITCHECK
    AUTOLOAD STDIN STDOUT STDERR ARGV ARGVOUT ENV INC SIG
};

sub import {
    my $class = shift;
    my ($name, $type) = @_;

    my $err;

    $err = $class->_validate_name($name);
    croak $err if $err;

    my $caller = caller;
    (my $kote, $err) = $class->_create_kote($name, $type, $caller);
    croak $err if $err;

    $err = $class->_add_kote($name, $kote, $caller);
    croak $err if $err;

    $err = $class->_setup_exporter($caller);
    croak $err if $err;
}

sub _validate_name {
    my ($class, $name) = @_;

    if (!$name) {
        return 'kote name is not given';
    }
    elsif ($name !~ $normal_kote_name) {
        return "kote name '$name' is not CamelCase.";
    }
    elsif ($forbidden_kote_name{$name}) {
        return "kote name '$name' is forbidden.";
    }

    return;
}

sub _to_type {
    my ($class, $type) = @_;

    Types::TypeTiny::to_TypeTiny($type);
}

sub _create_kote {
    my ($class, $name, $type, $caller) = @_;

    $type = $class->_to_type($type);
    unless (blessed($type) && $type->isa('Type::Tiny')) {
        return (undef, "$name: type must be able to be a Type::Tiny");
    }

    my $kote = Type::Kote->new(
        name   => $name,
        parent => $type,
        library => $caller,
    );

    # make kote immutable
    $kote->coercion->freeze;

    return ($kote, undef);
}

sub _add_kote {
    my ($class, $name, $kote, $caller) = @_;

    if ($caller->can($name)) {
        return "'$name' is already defined";
    }

    my $code = type_to_coderef($kote);

    {
        no strict "refs";
        *{"$caller\::$name"} = set_subname( "$caller\::$name", $code);
        push @{"$caller\::EXPORT_OK"}, $name;
        push @{ ${"$caller\::EXPORT_TAGS"}{types} ||= [] }, $name;
    }

    return;
}

sub _exporter_class {
    'Exporter::Tiny';
}

sub _setup_exporter {
    my ($class, $caller) = @_;

    my $exporter_class = $class->_exporter_class;

    unless ($caller->isa($exporter_class)) {
        no strict "refs";
        push @{ "$caller\::ISA" }, $exporter_class;
        ( my $file = $caller ) =~ s{::}{/}g;
        $INC{"$file.pm"} ||= __FILE__;
    }

    return;
}

1;
__END__

=encoding utf-8

=head1 NAME

kote - Type::Tiny based type framework

=head1 SYNOPSIS

    use Types::Standard -types;

    use kote CharacterName  => Str & sub { /^[A-Z][a-z]+$/ };
    use kote CharacterLevel => Int & sub { $_ >= 1 && $_ <= 100 };
    use kote Character => Dict[
        name => CharacterName,
        level => CharacterLevel,
    ];

    my ($alice, $err) = Character->create({ name => 'Alice', level => 1 });
    is $alice->{name}, 'Alice';

    my ($bob, $err) = Character->create({ name => 'bob', level => 0 });
    say $err; # Error

    Charcter->isa('Type::Tiny'); # true

=head1 DESCRIPTION

kote - B<means "gauntlet"ğŸ§¤ in Japanese> - is a type framework based on Type::Tiny.

=head2 FEATURES

=over 2

=item * å‹ã®å®£è¨€ãŒç°¡æ½”

å‹åã¨åˆ¶ç´„ã‚’ä¸€åº¦æ›¸ãã ã‘ã§ã€å‹ã‚’å®£è¨€ã§ãã¾ã™ã€‚

    use kote CharacterName => Str & sub { /^[A-Z][a-z]+$/ };

=item * å€¤ã®æ¤œè¨¼ãŒå®¹æ˜“

å€¤ãŒå‹ã‚’æº€ãŸã—ã¦ã„ã‚‹ã‹ç°¡å˜ã«æ¤œè¨¼ã§ãã¾ã™ã€‚

    my ($alice, $err) = CharacterName->create('Alice');

=item * Type::Tiny ãƒ™ãƒ¼ã‚¹

koteã§å®£è¨€ã—ãŸå‹ã¯ã€Type::Tinyã‚’ãƒ™ãƒ¼ã‚¹ã«ã—ã¦ã„ã‚‹ã®ã§ã€Type::Tinyã®æ©Ÿèƒ½ã‚’ãã®ã¾ã¾åˆ©ç”¨ã§ãã¾ã™ã€‚

    CharacterName->check('Alice'); # true

=back

=head1 CONCEPTS

koteã¯ã€æ¬¡ã®æ›¸ç±ã«è§¦ç™ºã•ã‚Œã¦ã„ã¾ã™ã€‚L<Domain Modeling Made Functional|https://pragprog.com/titles/swdddf/domain-modeling-made-functional/>
ãƒ‰ãƒ¡ã‚¤ãƒ³ç©ºé–“ã”ã¨ã«ã¨ã‚Šã†ã‚‹å€¤ã‚’å‹ã§å®£è¨€ã—ã€ãã®æŒ¯ã‚‹èˆã„ã‚’ç´”ç²‹é–¢æ•°ã§è¨˜è¿°ã—ã‚„ã™ãã§ããªã„ã‹è€ƒãˆã€ãƒ‡ã‚¶ã‚¤ãƒ³ã—ã¦ã„ã¾ã™ã€‚

=head1 DETAILS

=head2 declare type

koteã¯ã€å‹ã‚’å®£è¨€ã™ã‚‹ãŸã‚ã®æ§‹æ–‡ã‚’æä¾›ã—ã¾ã™ã€‚

    package My::Character;
    use kote CharacterName => Str & sub { /^[A-Z][a-z]+$/ };

å·¦è¾ºã®å‹åã¯CamelCaseã§ã‚ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚å³è¾ºã®åˆ¶ç´„ã¯ã€Type::Tinyã¯ã‚‚ã¡ã‚ã‚“ã®ã“ã¨ã€Type::Tinyã«ãªã‚Œã‚‹åˆ¶ç´„ã‚’æŒ‡å®šã§ãã¾ã™ã€‚
koteã‚’åˆ©ç”¨ã™ã‚‹ã¨Exporter::Tinyã‚’ç¶™æ‰¿ã—ã€å®£è¨€ã—ãŸå‹ã‚’ã€C<@EXPORT_OK>ã«è‡ªå‹•è¿½åŠ ã—ã¾ã™ã€‚
ã¤ã¾ã‚Šã€æ¬¡ã®ã‚ˆã†ã«å‹ã‚’ã‚¤ãƒ³ãƒãƒ¼ãƒˆã§ãã¾ã™ã€‚

    package main;
    use My::Character qw(CharacterName);

    CharacterName->check('Alice'); # true

=head2 create value

koteã§å®£è¨€ã—ãŸå‹ã¯ã€C<create>ãƒ¡ã‚½ãƒƒãƒ‰ã‚’æŒã¡ã¾ã™ã€‚

    my ($alice, $err) = Character->create({name => 'Alice', level => 1});
    croak $err if $err;

C<create>ãƒ¡ã‚½ãƒƒãƒ‰ã¯ã€ä¸ãˆã‚‰ã‚ŒãŸå€¤ãŒå‹ã‚’æº€ãŸã•ãªã„å ´åˆã¯ã‚¨ãƒ©ãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’è¿”ã—ã€æº€ãŸã™å ´åˆã¯ãã®å€¤ã‚’è¿”ã—ã¾ã™ã€‚
ãŸã ã—ã€å€¤ãŒãƒªãƒ•ã‚¡ãƒ¬ãƒ³ã‚¹ã ã£ãŸå ´åˆã¯ã€ä¸å¤‰ãªãƒªãƒ•ã‚¡ãƒ¬ãƒ³ã‚¹ã«å¤‰æ›ã—ã¦è¿”ã—ã¾ã™ã€‚

    $alice->{name} = 'Bob'; # Error
    $alice->{unknown}; # Error

ã¾ãŸã€ã‚¨ãƒ©ãƒ¼ãƒãƒ³ãƒ‰ãƒªãƒ³ã‚°ã‚’è¡Œã‚ãªã‹ã£ãŸå ´åˆã€ä¾‹å¤–ãŒç™ºç”Ÿã—ã¾ã™ã€‚

    my $alice = Character->create({name => 'Alice', level => 1});
    # => Must handle error!!

=head1 TIPS

=head2 export functions

é–¢æ•°ã®ã‚¨ã‚¯ã‚¹ãƒãƒ¼ãƒˆãŒã€C<@EXPORT_OK>ã«é–¢æ•°ã‚’è¿½åŠ ã™ã‚Œã°ã§ãã¾ã™ã€‚

    pakcage My::Character;

    our @EXPORT_OK;
    push @EXPORT_OK, qw(is_alice);

    use kote CharacterName => Str & sub { /^[A-Z][a-z]+$/ };

    sub is_alice($name) {
        # CharacterName->assert_valid($name);
        $name eq 'Alice';
    }

    package main;
    use My::Character qw(CharacterName is_alice);

=head2 skip check value

ãƒ‘ãƒ•ã‚©ãƒ¼ãƒãƒ³ã‚¹ã®éƒ½åˆã€å€¤ã®æ¤œè¨¼ã‚„ä¸å¤‰ãªãƒªãƒ•ã‚¡ãƒ¬ãƒ³ã‚¹ã¸ã®å¤‰æ›ã‚’ã‚¹ã‚­ãƒƒãƒ—ã—ãŸã„å ´åˆã€C<$kote::STRICT>ã‚’0ã«è¨­å®šã—ã¾ã™ã€‚
ãŸã ã—ã€æ¤œè¨¼ã™ã¹ãå€¤ã‚’ã‚¹ã‚­ãƒƒãƒ—ã—ãªã„ã‚ˆã†ã«ååˆ†æ³¨æ„ã—ã¦ãã ã•ã„ã€‚

    local $kote::STRICT = 0;
    my ($alice, $err) = CharacterName->create(1234);
    $err; # No Error

=head1 THANKS

L<Type::Tiny>ã®ä½œè€…ã€Toby Inksteræ°ã«æ„Ÿè¬ã—ã¾ã™ã€‚

=head1 LICENSE

Copyright (C) kobaken.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 AUTHOR

kobaken E<lt>kentafly88@gmail.comE<gt>

=cut

